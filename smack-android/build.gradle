description = """\
Smack for Android.
All the required dependencies to run Smack on Android"""

ext {
	smackMinAndroidSdk = 8
	androidProjects = [':smack-tcp',':smack-core', ':smack-resolver-minidns', ':smack-sasl-provided', ':smack-extensions', ':smack-experimental'].collect{ project(it) }
}

// Note that the test dependencies (junit, â€¦) are inferred from the
// sourceSet.test of the core subproject
dependencies {
	androidProjects.each { project ->
		compile project
	}
}

jar {
	enabled = false
}

def getAndroidRuntimeJar() {
	def androidHome = new File("$System.env.ANDROID_HOME")
	if (!androidHome.isDirectory()) throw new Exception("ANDROID_HOME not found or set")
	new File("$androidHome/platforms/android-$smackMinAndroidSdk/android.jar")
}

configure (androidProjects) {
	task compileAndroid(type: JavaCompile) {
		source = compileJava.source
		classpath = compileJava.classpath
		destinationDir = new File(buildDir, 'android')
		options.bootClasspath = getAndroidRuntimeJar()
	}
}

test { dependsOn androidProjects*.compileAndroid }