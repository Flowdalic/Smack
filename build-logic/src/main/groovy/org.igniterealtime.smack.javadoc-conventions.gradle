plugins {
	// Javadoc linking requires repositories to bet configured. And
	// those are declared in global-conventions, hence we add it here.
	id 'org.igniterealtime.smack.global-conventions'
}


tasks.withType(Javadoc) {
	// The '-quiet' as second argument is actually a hack,
	// since the one parameter addStringOption doesn't seem to
	// work, we extra add '-quiet', which is added anyway by
	// gradle.
	// We disable 'missing' as we do most of javadoc checking via checkstyle.
	options.addStringOption('Xdoclint:all,-missing', '-quiet')
	// Abort on javadoc warnings.
	// See JDK-8200363 (https://bugs.openjdk.java.net/browse/JDK-8200363)
	// for information about the -Xwerror option.
	options.addStringOption('Xwerror', '-quiet')
	options.addStringOption('-release', javaMajor)
}

// Workaround for gradle-maven-publish-plugin bug
// https://github.com/vanniktech/gradle-maven-publish-plugin/issues/1132
// provided by Bj√∂rn Kautler in
// https://discuss.gradle.org/t/partly-undo-the-configuration-performed-on-a-task-by-a-plugin/51533/3?u=flow
afterEvaluate {
    tasks.withType(Javadoc) {
		// This actually unsets "-Xdoclint:none" which is
		// unfortunately unconditionally added by gradle-maven-publish-plugin.
        options.addStringOption("Xdoclint:none")
    }
}

tasks.withType(Javadoc) {
	options.charSet = "UTF-8"
}
